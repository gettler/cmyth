#!/usr/bin/python
#
# cmyth apps
#

Import('env')

ctg = env['CTYPESGEN']

libs = [ 'cmyth', 'pthread', 'refmem', ]

if not env.GetOption('clean'):
    conf = Configure(env)
    if conf.CheckLib('mysqlclient'):
        libs += [ 'mysqlclient' ]
    env = conf.Finish()

if ctg != '':
    ctypesgen = env.Command([ 'cmyth_ffi.py' ],
                            [ Glob('include/*.h'),
                              Glob('include/*/*.h') ],
                            [ 'ctypesgen.py -lrefmem -lcmyth include/*.h include/refmem/*.h include/cmyth/*.h -o src/cmyth_ffi.py' ])
    env.Alias('ctypesgen', [ ctypesgen ])

mythfuse = env.Program('mythfuse', 'mythfuse.c',
                       CCFLAGS = ' -D_FILE_OFFSET_BITS=64',
                       CPPPATH = [ '../include' ],
                       LIBS = libs + [ 'fuse' ],
                       LIBPATH = [ '../libcmyth', '../librefmem' ])

mythping = env.Program('mythping', 'mythping.c',
                       CPPPATH = [ '../include' ],
                       LIBS = libs,
                       LIBPATH = [ '../libcmyth', '../librefmem' ])

mythcat = env.Program('mythcat', 'mythcat.c',
                      CPPPATH = [ '../include' ],
                      LIBS = libs,
                      LIBPATH = [ '../libcmyth', '../librefmem' ])

targets = [ mythping, mythcat ]

if ctg != '':
    targets += [ ctypesgen ]

if not env.GetOption('clean'):
    conf = Configure(env)
    if conf.CheckLib('fuse'):
        targets += [ mythfuse ]
    env = conf.Finish()
else:
    targets += [ mythfuse ]

prefix = env['PREFIX']
env.Install(prefix + '/bin', targets)

if not env.GetOption('clean') and ctg != '':
    env.Install(prefix + '/bin', [ 'cmyth.py', 'mythinfo.py', 'mythplayer.py' ])

Return('targets')
