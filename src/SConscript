#!/usr/bin/python
#
# cmyth apps
#

import os

Import('env')

prefix = env['PREFIX']

libs = [ 'cmyth', 'pthread', 'refmem', ]
cpplibs = [ 'cppmyth', 'cmyth', 'pthread', 'refmem' ]
libpath = [ '../libcmyth', '../libcppmyth', '../librefmem' ]

if not env.GetOption('clean'):
    conf = Configure(env)
    if conf.CheckLib('mysqlclient'):
        libs += [ 'mysqlclient' ]
    env = conf.Finish()

mythfuse = env.Program('mythfuse', 'mythfuse.c',
                       CCFLAGS = env['CCFLAGS'] + ' -D_FILE_OFFSET_BITS=64',
                       CPPPATH = [ '../include' ],
                       LIBS = libs + [ 'fuse' ],
                       LIBPATH = libpath)

mythping = env.Program('mythping', 'mythping.c',
                       CPPPATH = [ '../include' ],
                       LIBS = libs,
                       LIBPATH = libpath)

mythcat = env.Program('mythcat', 'mythcat.c',
                      CPPPATH = [ '../include' ],
                      LIBS = libs,
                      LIBPATH = libpath)

targets = [ mythping, mythcat ]

if not env.GetOption('clean'):
    conf = Configure(env)
    if conf.CheckLib('fuse'):
        targets += [ mythfuse ]
    env = conf.Finish()
else:
    targets += [ mythfuse ]

prefix = env['PREFIX']
env.Install(prefix + '/bin', targets)
env.Install(prefix + '/bin', [ 'mythinfo.py', 'mythplayer.py',
                               'mythtranscode.py' ])

Return('targets')
