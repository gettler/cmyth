#
# cmyth apps
#

import os

Import('env')

prefix = env['PREFIX']

libs = [ 'cmyth', 'pthread', 'refmem', ]
cpplibs = [ 'cppmyth', 'cmyth', 'pthread', 'refmem' ]
libpath = [ '../libcmyth', '../libcppmyth', '../librefmem' ]
targets = [ ]

conf = Configure(env)
if conf.CheckLib('mysqlclient'):
    libs += [ 'mysqlclient' ]
if conf.CheckLib('fuse'):
    mythfuse = env.Program('mythfuse', 'mythfuse.c',
                           CCFLAGS = env['CFLAGS'] + ' -D_FILE_OFFSET_BITS=64',
                           LINKFLAGS = env['LDFLAGS'],
                           CPPPATH = [ '../include' ],
                           LIBS = libs + [ 'fuse' ],
                           LIBPATH = libpath)
    targets += [ mythfuse ]
env = conf.Finish()

mythping = env.Program('mythping', 'mythping.c',
                       LINKFLAGS = env['LDFLAGS'],
                       CPPPATH = [ '../include' ],
                       LIBS = libs,
                       LIBPATH = libpath)

mythcat = env.Program('mythcat', 'mythcat.c',
                      LINKFLAGS = env['LDFLAGS'],
                      CPPPATH = [ '../include' ],
                      LIBS = libs,
                      LIBPATH = libpath)

targets += [ mythping, mythcat ]

env.Install(prefix + '/bin', targets)
env.Install(prefix + '/bin', [ 'mythinfo.py', 'mythplayer.py',
                               'mythtranscode.py' ])

Return('targets')
