#
# SWIG - Simplified Wrapper and Interface Generator
#
# Generate interfaces to libcmyth for other languages.
#

import os
import sys
import shutil
from distutils import sysconfig

Import('env')

prefix = env['PREFIX']
platform = env['PLATFORM']

targets = []

interface = [ 'cmyth.i', Glob('../include/*.h'), Glob('../include/*/*.h') ]
cmd = 'swig -c++ -%s -Iinclude -o swig/%s swig/cmyth.i'
cmd_java = 'swig -c++ -%s -package %s -outdir %s -o swig/%s -Iinclude swig/cmyth.i'

conf = Configure(env)

#
# Python
#
if env.swig_use_python():
    libs = [ 'cppmyth', 'cmyth', 'refmem' ]
    if sys.platform == 'darwin':
        libs += [ 'python' ]
    python = env.Command([ 'cmyth_python.cpp', 'cmyth.py' ],
                         interface,
                         [ cmd % ('python', 'cmyth_python.cpp') ])
    targets += python
    shared = env.SharedLibrary('cmyth_python',
                               [ 'cmyth_python.cpp' ],
                               SHLIBSUFFIX = '.so',
                               LIBS = libs,
                               CCFLAGS = [],
                               CPPPATH = [ '../include', '.',
                                           sysconfig.get_python_inc() ],
                               LIBPATH = [ '../libcppmyth',
                                           '../libcmyth',
                                           '../librefmem' ])
    targets += shared
    env.Install(prefix + '/lib/python', [ 'cmyth.py' ])
    env.Install(prefix + '/lib', shared)
    if 'install' in COMMAND_LINE_TARGETS:
        env.Command(prefix + '/lib/python/_cmyth.so', shared,
                    'ln -sf ../libcmyth_python.so %s/%s' % (prefix,
                                                            'lib/python/_cmyth.so'))

#
# PHP
#
if env.swig_use_php():
    php = env.Command([ 'cmyth_php.cpp', 'php_cmyth.h', 'cmyth.php' ],
                      interface,
                      [ cmd % ('php', 'cmyth_php.cpp') ])
    targets += php
    rc,phpinc,err = env.run_command('php-config --include-dir')
    phpinc = phpinc[:-1]
    if sys.platform == 'darwin':
        undefs = [ '_zend_register_resource', '_zend_rsrc_list_get_rsrc_type',
                   '_zend_wrong_param_count', '_compiler_globals',
                   '_zend_hash_find', '_gc_remove_zval_from_buffer',
                   '_zend_register_long_constant', '_zend_get_constant',
                   '__efree', '__emalloc', '_zend_error', '__zend_list_find',
                   '_main', '__zend_get_parameters_array_ex',
                   '_zend_register_list_destructors_ex',
                   '__zval_copy_ctor_func', '__convert_to_string',
                   '__zend_hash_add_or_update', '_executor_globals',
                   '_zval_used_for_init', '_zval_is_true', '__object_init',
                   '_php_strtolower', '__estrndup', '__object_init_ex',
                   '_zend_lookup_class', '_convert_to_long',
                   '_zend_throw_exception',
                   '_add_index_stringl' ]
        linkflags = '-Wl,-flat_namespace,-U,' + ',-U,'.join(undefs)
    else:
        linkflags = ''
    shared = env.SharedLibrary('cmyth_php',
                               [ 'cmyth_php.cpp' ],
                               SHLIBSUFFIX = '.so',
                               LIBS = [ 'cppmyth', 'cmyth', 'refmem' ],
                               CCFLAGS = [],
                               LINKFLAGS = linkflags,
                               CPPPATH = [ '../include', '.',
                                           phpinc,
                                           phpinc + '/main',
                                           phpinc + '/TSRM',
                                           phpinc + '/Zend' ],
                               LIBPATH = [ '../libcppmyth',
                                           '../libcmyth',
                                           '../librefmem' ])
    targets += shared
    env.Install(prefix + '/lib', shared)
    env.Install(prefix + '/lib/php', 'cmyth.php')
    if 'install' in COMMAND_LINE_TARGETS:
        env.Command(prefix + '/lib/php/_cmyth.so', shared,
                    'ln -sf ../libcmyth_php.so %s/%s' % (prefix,
                                                         'lib/php/cmyth.so'))

#
# Ruby
#
if env.swig_use_ruby():
    rc,rubyarch,err = env.run_command('ruby -rrbconfig -e '
                                      '\'puts Config::CONFIG["archdir"]\'')
    libs = [ 'cppmyth', 'cmyth', 'refmem' ]
    suffix = '.so'
    if sys.platform == 'darwin':
        libs += [ 'ruby' ]
        suffix = '.bundle'
    ruby = env.Command([ 'cmyth_ruby.cpp' ],
                       interface,
                       [ cmd % ('ruby', 'cmyth_ruby.cpp') ])
    targets += ruby
    shared = env.SharedLibrary('cmyth_ruby',
                               [ 'cmyth_ruby.cpp' ],
                               SHLIBSUFFIX = '.so',
                               CCFLAGS = [],
                               LIBS = libs,
                               CPPPATH = [ '../include', '.',
                                           rubyarch[:-1] ],
                               LIBPATH = [ '../libcppmyth',
                                           '../libcmyth',
                                           '../librefmem' ])
    targets += shared
    env.Install(prefix + '/lib', shared)
    if 'install' in COMMAND_LINE_TARGETS:
        env.Command(prefix + '/lib/ruby/cmyth' + suffix, shared,
                    'ln -sf ../libcmyth_ruby.so %s/%s' %
                    (prefix, 'lib/ruby/cmyth' + suffix))

#
# Java
#
if env.swig_use_java():
    javapath = env['JAVA_HOME']
    if os.path.isfile(javapath + '/include/jni.h') or \
            os.path.isfile(javapath + '/Headers/jni.h'):
        path = 'org/mvpmc/cmyth/java'
        try:
            os.makedirs(path)
        except:
            pass
        bases = [ 'cmyth', 'cmythJNI', 'cmythConstants',
                  'SWIGTYPE_p_cmyth_conn_t',
                  'SWIGTYPE_p_cmyth_proglist_t',
                  'SWIGTYPE_p_cmyth_proginfo_t',
                  'filetype_t',
                  'connection', 'exception', 'proginfo', 'proglist',
                  'refmem', 'file' ]
        src_list = []
        for i in bases:
            src_list.append(path + '/' + i + '.java')
        src = ''
        for i in src_list:
            src = src + ' ' + i
        classes = ''
        for i in bases:
            classes = classes + path + '/' + i + '.class '
        src_list.append('cmyth_java.cpp')
        java = env.Command(src_list,
                           interface,
                           [ cmd_java % ('java', 'org.mvpmc.cmyth.java',
                                         'swig/' + path, 'cmyth_java.cpp') ])
        targets += java
        if sys.platform == 'darwin':
            suffix = '.jnilib'
            linkflags = [ 'librefmem/librefmem.a',
                          'libcmyth/libcmyth.a',
                          'libcppmyth/libcppmyth.a' ]
            libs = [ ]
            libpath = [ ]
        else:
            suffix = '.so'
            linkflags = [ ]
            libs = [ 'cppmyth', 'cmyth', 'refmem' ]
            libpath = [ '../libcppmyth', '../libcmyth', '../refmem' ]
        if platform == 'android':
            makefile = env.NDK('cmyth_java', 'cmyth_java.cpp',
                               LIBS = [ 'cppmyth', 'refmem' ],
                               LIBDIRS = [ '../libcppmyth', '../librefmem' ],
                               INCLUDES = [ '../include', '.' ])
            targets += makefile
            env.Install(prefix + '/lib', makefile)
        else:
            shared = env.SharedLibrary('cmyth_java',
                                       [ 'cmyth_java.cpp' ],
                                       SHLIBSUFFIX = suffix,
                                       CCFLAGS = [],
                                       LINKFLAGS = linkflags,
                                       LIBS = libs,
                                       LIBPATH = libpath,
                                       CPPPATH = [ '../include', '.',
                                                   javapath + '/include',
                                                   javapath + '/Headers', ])
            targets += shared
        output = env.Command(path + '/cmyth.class',
                             java,
                             [ 'javac -d %s -sourcepath %s %s' % ('.',
                                                                  path,
                                                                  src) ],
                             chdir='swig')
                                  
        jar = env.Command('cmyth.jar', output,
                          [ 'jar cf cmyth.jar %s' % classes ],
                          chdir='swig')
        targets += jar

        if not platform == 'android':
            env.Install(prefix + '/lib', shared)
        env.Install(prefix + '/lib', jar)

        if env.GetOption('clean'):
            try:
                shutil.rmtree('org')
            except:
                pass

env = conf.Finish()

Return('targets')
