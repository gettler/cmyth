import shutil

Import('env')

targets = []

cpplibs = [ 'cppmyth', 'cmyth', 'pthread', 'refmem' ]
libpath = [ '../libcmyth', '../libcppmyth', '../librefmem' ]
ccflags = env['CCFLAGS']

if 'CMD_JAVAC' in env and 'JAVA_HOME' in env:
    java_test = env.Java('test_java.class', 'test_java.java',
                         JAVACLASSPATH='swig')
    targets += java_test
    if env.GetOption('clean'):
        try:
            shutil.rmtree('test_java.class')
        except:
            pass

if not env.GetOption('clean'):
    conf = Configure(env)
    if conf.CheckLib('ssl', 'MD5_Init'):
        cpplibs += [ 'ssl' ]
        ccflags = ccflags + ' -DWITH_SSL'
    env = conf.Finish()

cpp_test = env.Program('test_cpp', 'test_cpp.cpp',
                       CPPPATH = [ '../include' ],
                       CCFLAGS = ccflags,
                       LIBS = cpplibs,
                       LIBPATH = libpath)

targets += cpp_test

Return('targets')
