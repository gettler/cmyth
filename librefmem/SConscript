#!/usr/bin/python
#
# librefmem
#

import sys

Import('env')

src = [ 'alloc.c', 'debug.c' ]

major = '0'
minor = '1'
version = major + '.' + minor
soname = 'librefmem.so.' + version
soname2 = 'librefmem.so.' + major
soname3 = 'librefmem.so'

if sys.platform == 'darwin':
    linkflags = [ ]
    suffix = '.dylib'
else:
    linkflags = [ '-Wl,-soname,' + soname ]
    suffix = '.so.' + version

static = env.StaticLibrary('refmem', src,
                           LINKFLAGS = linkflags,
                           LIBS = [],
                           CPPPATH = [ '../include', '.' ])
shared = env.SharedLibrary('refmem', src,
                           SHLIBSUFFIX = suffix,
                           LINKFLAGS = linkflags,
                           LIBS = [],
                           CPPPATH = [ '../include', '.' ])

if sys.platform == 'darwin':
    targets = [ static, shared ]
else:
    link2 = env.Command(soname2, soname,
                        'ln -sf %s %s' % (soname, soname2), chdir=1)
    link3 = env.Command(soname3, soname2,
                        'ln -sf %s %s' % (soname2, soname3), chdir=1)

    targets = [ static, shared, link2, link3 ]

    env.Depends(static, link2)
    env.Depends(static, link3)

prefix = env['PREFIX']
install = env.Install(prefix + '/lib', targets)

if sys.platform != 'darwin':
    symlink2 = env.Command(prefix + '/lib/' + soname2, link2,
                           'ln -sf %s %s' % (soname, prefix + '/lib/' + soname2))
    symlink3 = env.Command(prefix + '/lib/' + soname3, link3,
                           'ln -sf %s %s' % (soname2, prefix + '/lib/' + soname3))

Return('targets')
