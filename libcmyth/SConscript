#
# libcmyth
#

import sys

Import('env')

prefix = env['PREFIX']

name = 'cmyth'
major = 0
minor = 1
branch = 1

src = [ 'bookmark.c', 'connection.c', 'debug.c', 'event.c',
        'file.c', 'freespace.c', 'keyframe.c',
        'posmap.c', 'proginfo.c', 'proglist.c', 'rec_num.c', 
        'recorder.c', 'ringbuf.c', 'socket.c', 'timestamp.c',
        'livetv.c', 'commbreak.c' ]

conf = Configure(env)
if conf.CheckCHeader('mysql/mysql.h'):
    conf.env.Append(CPPFLAGS = '-DHAS_MYSQL')
    src += [ 'mythtv_mysql.c', 'mysql_query.c' ]
env = conf.Finish()

linkflags = env.soname(name, major, minor, branch)

static = env.StaticLibrary(name, src,
                           LIBS = [],
                           CPPPATH = [ '../include', '.' ])

shared = env.SharedLibrary(name, src,
                           SHLIBSUFFIX = env.shlibsuffix(major, minor, branch),
                           LINKFLAGS = linkflags,
                           LIBS = [],
                           CPPPATH = [ '../include', '.' ])

link0 = env.Symlink('lib%s%s' % (name, env.shlibsuffix(major, minor)), shared)
link1 = env.Symlink('lib%s%s' % (name, env.shlibsuffix(major)), shared)
link2 = env.Symlink('lib%s%s' % (name, env.shlibsuffix()), shared)

targets = [ static, shared, link0, link1, link2 ]

env.Install(prefix + '/lib', static)

lib0 = env.Install(prefix + '/lib', shared)
lib1 = env.Symlink('%s/lib/lib%s%s' % (prefix, name,
                                       env.shlibsuffix(major, minor)), lib0)
lib2 = env.Symlink('%s/lib/lib%s%s' % (prefix, name,
                                       env.shlibsuffix(major)), lib1)
lib3 = env.Symlink('%s/lib/lib%s%s' % (prefix, name,
                                       env.shlibsuffix()), lib2)

Return('targets')
